import QtQuick
import Quickshell.Io
import qs.Common
import qs.Services
import qs.Modules.FilePicker
import qs.Modules.Keystone.Styles.Bangs
import qs.Modules.Keystone.Styles.Pill

Item {
    id: root

    readonly property bool searchActionsAvailable: styleLoader.item !== null

    function invoke(methodName): string {
        if (!styleLoader.item || typeof styleLoader.item[methodName] !== "function")
            return "KEYSTONE_UNAVAILABLE";
        return styleLoader.item[methodName]();
    }

    function openAvatarPicker(screen) {
        avatarFilePicker.targetScreen = screen;
        Qt.callLater(() => avatarFilePicker.openAt(avatarFilePicker.picturesDir !== ""
                                                   ? avatarFilePicker.picturesDir : Paths.homeDir));
    }

    Loader {
        id: styleLoader

        sourceComponent: PersonalizationConfig.keystoneStyle === "pill" ? pillStyle : bangsStyle
    }

    FilePickerWindow {
        id: avatarFilePicker

        dialogTitle: qsTr("Choose user avatar")
        description: qsTr("The image will be copied to ~/.face and used by the Dashboard and lock screen")
        onAccepted: path => AvatarService.setAvatar(path)
    }

    IpcHandler {
        target: "keystone"

        function cancelRecord(): string {
            return root.invoke("cancelRecord");
        }
        function closeAllOthers(): string {
            return root.invoke("closeAllOthers");
        }
        function currentStyle(): string {
            return PersonalizationConfig.keystoneStyle;
        }
        function dashboard(): string {
            return root.invoke("dashboard");
        }
        function hub(): string {
            return root.invoke("hub");
        }
        function lyrics(): string {
            return root.invoke("lyrics");
        }
        function tools(): string {
            return root.invoke("tools");
        }
    }

    Component {
        id: bangsStyle

        Bangs {
            onAvatarEditRequested: screen => root.openAvatarPicker(screen)
        }
    }

    Component {
        id: pillStyle

        Pill {
            onAvatarEditRequested: screen => root.openAvatarPicker(screen)
        }
    }
}
