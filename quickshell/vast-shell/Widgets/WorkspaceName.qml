pragma ComponentBehavior: Bound

import QtQuick
import QtQuick.Layouts
import Quickshell.Wayland

import qs.Core.Configs
import qs.Services

import "../Components/Base"

StyledRect {
    id: root

    FontMetrics {
        id: windowNameMetrics

        font: windowNameText.font
    }

    Layout.fillHeight: true
    implicitWidth: windowNameMetrics.advanceWidth(windowNameText.text)
    color: "transparent"

    Behavior on implicitWidth {
        NAnim {
            duration: Appearance.animations.durations.small
            easing.bezierCurve: Appearance.animations.curves.expressiveFastSpatial
        }
    }

    StyledText {
        id: windowNameText

        anchors.centerIn: parent

        readonly property Toplevel activeWindow: ToplevelManager.activeToplevel
        property string actWinName: activeWindow?.activated ? activeWindow?.appId : "desktop"

        color: Colours.m3Colors.m3OnBackground
        elide: Text.ElideMiddle
        font.weight: Font.Light
        font.pixelSize: Appearance.fonts.size.large
        horizontalAlignment: Text.AlignHCenter
        text: actWinName.toUpperCase()
    }
}
